import measure_theory.integral_eq_improper
import analysis.special_functions.exp_log
import analysis.special_functions.trigonometric
import analysis.calculus.parametric_integral
import topology.uniform_space.compact_separated

noncomputable theory

open interval_integral set filter measure_theory
open_locale topological_space

def f : â„ â†’ â„ := Î» x, âˆ« t in 0..x, real.exp (-t^2)

def g : â„ â†’ â„ := Î» x, âˆ« t in 0..1, (real.exp (-(1+t^2)*x^2))/(1+t^2)

def h : â„ â†’ â„ := Î» x, g x + (f^2) x

lemma is_const_of_deriv_eq_zero {F : Type*} 
  [normed_group F] [normed_space â„ F] {f : â„ â†’ F} (hf : differentiable â„ f) 
  (hz : âˆ€ x, deriv f x = 0) : âˆ€ x y, f x = f y :=
begin
  apply is_const_of_fderiv_eq_zero hf (Î» x, _),
  rw [â† deriv_fderiv, hz x],
  ext u,
  simp
end

lemma exists_has_deriv_at_eq_slope_interval (f f' : â„ â†’ â„) {a b : â„}
  (hab : a â‰  b) (hf : continuous_on f (interval a b))
  (hff' : âˆ€ (x : â„), x âˆˆ Ioo (min a b) (max a b) â†’ has_deriv_at f (f' x) x) :
  (âˆƒ (c : â„) (H : c âˆˆ Ioo (min a b) (max a b)), f' c = (f b - f a) / (b - a)) :=
begin
  by_cases hlt : a < b;
  [skip, have hlt : b < a := lt_of_le_of_ne (le_of_not_lt hlt) hab.symm];
  rw interval at *;
  [rw min_eq_left_of_lt hlt at *, rw min_eq_right_of_lt hlt at *];
  [rw max_eq_right_of_lt hlt at *, rw max_eq_left_of_lt hlt at *];
  convert exists_has_deriv_at_eq_slope f f' hlt hf hff',
  conv in (_ / _) { rw [â† neg_div_neg_eq, neg_sub, neg_sub] }
end

lemma has_deriv_at_parametric {a b : â„} (hab : a < b) (f f' : â„ â†’ â„ â†’ â„) 
  (hff' : âˆ€ x t, has_deriv_at (Î» u, f u t) (f' x t) x)
  (hf : continuous â†¿f) (hf' : continuous â†¿f') (xâ‚€ : â„) :
has_deriv_at (Î» x, âˆ« t in a..b, f x t) (âˆ« t in a..b, f' xâ‚€ t) xâ‚€ :=
begin
  refine has_deriv_within_at.has_deriv_at _ (Icc_mem_nhds (sub_one_lt xâ‚€) (lt_add_one xâ‚€)),
  have compact_ab : is_compact (interval a b) := is_compact_Icc,
  have compact_cd : is_compact (Icc (xâ‚€-1) (xâ‚€+1)) := is_compact_Icc,
  have := (compact_cd.prod compact_ab).uniform_continuous_on_of_continuous hf'.continuous_on,
  rw [has_deriv_within_at_iff_tendsto_slope, metric.tendsto_nhds_within_nhds],
  intros Îµ hÎµ,
  rw metric.uniform_continuous_on_iff at this,
  specialize this ((Îµ/2)/(b-a)) (div_pos (by linarith) (by linarith)),
  rcases this with âŸ¨Î´, hÎ´, thisâŸ©,
  use [Î´, hÎ´],
  rintros yâ‚€ âŸ¨âŸ¨hyâ‚, hyâ‚‚âŸ©, hyâ‚ƒ : yâ‚€ â‰  xâ‚€âŸ© hyâ‚€xâ‚€,
  have keyâ‚ := Î» (t : â„), exists_has_deriv_at_eq_slope_interval (Î» x, f x t) (Î» x, f' x t) hyâ‚ƒ.symm 
    (@continuous_uncurry_right _ _ _ _ _ _ f t hf).continuous_on (Î» x hx, hff' x t),
  choose u hu using keyâ‚,
  have keyâ‚‚ : âˆ€ t, dist (u t) xâ‚€ < Î´,
  { intro t,
    rw [real.dist_eq, abs, max_lt_iff] at âŠ¢ hyâ‚€xâ‚€,
    rcases hu t with âŸ¨âŸ¨htâ‚, htâ‚‚âŸ©, _âŸ©,
    by_cases hl : xâ‚€ < yâ‚€;
    [skip, push_neg at hl];
    [rw min_eq_left_of_lt hl at htâ‚, rw min_eq_right hl at htâ‚];
    [rw max_eq_right_of_lt hl at htâ‚‚, rw max_eq_left hl at htâ‚‚];
    split;
    linarith },
  have keyâ‚ƒ : Ioo (min xâ‚€ yâ‚€) (max xâ‚€ yâ‚€) âŠ† Icc (xâ‚€-1) (xâ‚€+1),
  { by_cases hl : xâ‚€ < yâ‚€;
    [skip, push_neg at hl];
    [rw min_eq_left_of_lt hl, rw min_eq_right hl];
    [rw max_eq_right_of_lt hl, rw max_eq_left hl];
    rintros a âŸ¨haâ‚, haâ‚‚âŸ©;
    split;
    linarith },
  have keyâ‚„ : âˆ€ x, continuous (f x) := Î» x, @continuous_uncurry_left _ _ _ _ _ _ f x hf,
  have keyâ‚… : continuous (Î» (t : â„), (yâ‚€ - xâ‚€)â»Â¹ * (f yâ‚€ t - f xâ‚€ t)) :=
    continuous_const.mul ((keyâ‚„ yâ‚€).sub (keyâ‚„ xâ‚€)),
  have keyâ‚…' : continuous (Î» (t : â„), (yâ‚€ - xâ‚€)â»Â¹ â€¢ (f yâ‚€ t - f xâ‚€ t)) := keyâ‚…,
  have keyâ‚† : continuous (Î» (t : â„), f' (u t) t),
  { convert keyâ‚…,
    ext t,
    rw [(hu t).2, div_eq_mul_inv, mul_comm] },
  rw [â† integral_sub ((keyâ‚„ yâ‚€).interval_integrable _ _) ((keyâ‚„ xâ‚€).interval_integrable _ _), 
      â† interval_integral.integral_smul, real.dist_eq, 
      â† integral_sub (keyâ‚…'.interval_integrable _ _)
        ((@continuous_uncurry_left _ _ _ _ _ _ f' xâ‚€ hf').interval_integrable _ _)],
  conv in (_ â€¢ _)
  { dsimp, rw â† div_eq_inv_mul, rw â† (hu _).2 },
  calc abs (âˆ« (t : â„) in a..b, f' (u t) t - f' xâ‚€ t) 
      â‰¤ abs (âˆ« (t : â„) in a..b, abs (f' (u t) t - f' xâ‚€ t)) : 
        by rw â† real.norm_eq_abs; exact norm_integral_le_abs_integral_norm
  ... = abs (âˆ« (t : â„) in Ioc a b, abs (f' (u t) t - f' xâ‚€ t)) : 
        by repeat {conv in (interval_integral _ _ _ _) { rw integral_of_le hab.le }}
  ... = (âˆ« (t : â„) in Ioc a b, abs (f' (u t) t - f' xâ‚€ t)) : 
        by rw abs_eq_self; exact integral_nonneg (Î» x, abs_nonneg _)
  ... â‰¤ (âˆ« (t : â„) in Ioc a b, (Îµ/2)/(b-a)) : 
        begin
          have meas_ab : measurable_set (Ioc a b) := measurable_set_Ioc,
          rw â† integral_indicator meas_ab,
          rw â† integral_indicator meas_ab,
          refine integral_mono _ _ (Î» t, _),
          { rw integrable_indicator_iff meas_ab,
            refine ((continuous_abs.comp _).integrable_on_compact is_compact_Icc).mono_set Ioc_subset_Icc_self,
            refine keyâ‚†.sub (@continuous_uncurry_left _ _ _ _ _ _ f' xâ‚€ hf') },
          { rw integrable_indicator_iff meas_ab,
            refine (continuous.integrable_on_compact compact_Icc _).mono_set Ioc_subset_Icc_self,
            simp only [continuous_const]},
          by_cases ht : t âˆˆ Ioc a b,
          { have := le_of_lt (this (u t, t) (xâ‚€, t) _ _ _),
            { simpa only [ht, indicator_of_mem] using this},
            { refine âŸ¨keyâ‚ƒ (hu t).1, Icc_subset_interval $ Ioc_subset_Icc_self htâŸ©, },
            { exact âŸ¨âŸ¨(sub_one_lt xâ‚€).le, (lt_add_one xâ‚€).leâŸ©, 
                Icc_subset_interval $ Ioc_subset_Icc_self htâŸ© },
            { rw [prod.dist_eq, max_lt_iff],
              refine âŸ¨keyâ‚‚ t, (dist_self t).symm â–¸ hÎ´âŸ© } },
          { simp only [ht, indicator_of_not_mem, not_false_iff]},
        end
  ... = (b-a) * (Îµ/2/(b-a)) :
        by rw [set_integral_const, real.volume_Ioc,
               ennreal.to_real_of_real (show 0 â‰¤ b - a, by linarith),
               smul_eq_mul]
  ... = Îµ/2 : mul_div_cancel' (Îµ/2) (show b - a â‰  0, by linarith)
  ... < Îµ : by linarith [hÎµ],
  all_goals {apply_instance}
end

lemma continuous_gauss : continuous (Î» x, real.exp (-x^2)) := by continuity; exact complex.continuous_exp

lemma has_deriv_at_f (x : â„) : has_deriv_at f (real.exp (-x^2)) x := 
integral_has_deriv_at_right (continuous_gauss.interval_integrable _ _) 
  continuous_gauss.measurable.measurable_at_filter continuous_gauss.continuous_at

lemma has_deriv_at_f_square (x : â„) : has_deriv_at (f^2) (2 * real.exp (-x^2) * âˆ« t in 0..x, real.exp (-t^2)) x := 
begin
  convert has_deriv_at.comp x (has_deriv_at_pow 2 _) (has_deriv_at_f x) using 1,
  norm_cast,
  ring
end

lemma has_deriv_at_g (xâ‚€ : â„) : has_deriv_at g (âˆ« t in 0..1, -2 * xâ‚€ * real.exp (-(1+t^2)*xâ‚€^2)) xâ‚€ := 
begin
  have keyâ‚ : continuous (Î» (t : â„), 1 + t^2) := by continuity,
  have keyâ‚‚ : continuous â†¿(Î» x t, real.exp (-(1+t^2) * x^2)) :=
    real.continuous_exp.comp ((keyâ‚.comp continuous_snd).neg.mul 
      ((continuous_pow 2).comp continuous_fst)),
  apply has_deriv_at_parametric (zero_lt_one),
  { intros x t,
    have stepâ‚ : has_deriv_at (Î» u, -(1 + t^2) * u^2) (-(1 + t^2) * 2 * x) x,
    { convert (has_deriv_at_pow 2 x).const_mul (-(1 + t ^ 2)) using 1,
      norm_cast,
      ring },
    have stepâ‚‚ : has_deriv_at (Î» u, real.exp (-(1 + t^2) * u^2)) 
      (-(1 + t^2) * 2 * x * real.exp (-(1 + t^2) * x^2)) x,
    { rw mul_comm, 
      exact has_deriv_at.comp x (real.has_deriv_at_exp _) stepâ‚ },
    conv in (_ / _) { rw div_eq_mul_inv },
    convert stepâ‚‚.mul_const _ using 1,
    conv_rhs {rw mul_comm, rw â† mul_assoc, rw â† mul_assoc, rw â† mul_assoc, 
              rw mul_neg_eq_neg_mul_symm, 
              rw inv_mul_cancel (show 1 + t^2 â‰  0, by linarith [pow_two_nonneg t]) },
    ring },
  { exact keyâ‚‚.div (keyâ‚.comp continuous_snd) (Î» âŸ¨x, tâŸ©, by linarith [pow_two_nonneg t]) },
  { exact (continuous_const.mul continuous_fst).mul keyâ‚‚, },
end

lemma key1 : âˆ€ x : â„, âˆ€ t âˆˆ (set.interval 0 1 : set â„), 
  has_deriv_at ((Î» (u : â„), âˆ« (t : â„) in 0..u, real.exp (-t ^ 2)) âˆ˜ Î» (u : â„), u * x) (real.exp (-(t * x) ^ 2) * x) t :=
begin
  intros x t ht,
  apply has_deriv_at.comp _ (has_deriv_at_f _),
  convert (has_deriv_at_id t).mul_const x, 
  ring
end

lemma has_deriv_at_h : âˆ€ x, has_deriv_at h 0 x :=
begin
  intros x,
  rw h,
  convert â† (has_deriv_at_g x).add (has_deriv_at_f_square x),
  rw add_eq_zero_iff_eq_neg,
  calc  âˆ« (t : â„) in 0..1, (-2) * x * real.exp (-(1 + t ^ 2) * x ^ 2)
      = âˆ« (t : â„) in 0..1, (-2) * x * real.exp (-(t * x) ^ 2 + -x ^ 2) :
        by conv in (-(1+_^2)*x^2) { ring_nf, rw [â† mul_pow, mul_comm, sub_eq_add_neg] }
  ... = âˆ« t in 0..1, (-2) * x * (real.exp (-x ^ 2) * real.exp (-(t * x) ^ 2)) : 
        by conv in (real.exp _) { rw real.exp_add, rw mul_comm }
  ... = âˆ« t in 0..1, (-2 * real.exp (-x ^ 2)) * (real.exp (-(t * x) ^ 2) * x) : 
        by congr; ext t; ring
  ... = âˆ« t in 0..1, (-2 * real.exp (-x ^ 2)) â€¢ (real.exp (-(t * x) ^ 2) * x) : 
        by congr; ext t; rw smul_eq_mul
  ... = (-2 * real.exp (-x ^ 2)) â€¢ âˆ« t in 0..1, (real.exp (-(t * x) ^ 2) * x) : 
        integral_smul _
  ... = (-2 * real.exp (-x ^ 2)) * âˆ« t in 0..1, (real.exp (-(t * x) ^ 2) * x) : 
        by rw smul_eq_mul
  ... = (-2 * real.exp (-x ^ 2)) *
          ( ((Î» u, âˆ« t in 0..u, real.exp (-t ^ 2)) âˆ˜ (Î» u, u * x)) 1
          - ((Î» u, âˆ« t in 0..u, real.exp (-t ^ 2)) âˆ˜ (Î» u, u * x)) 0 ) : 
        begin
          congr,
          refine integral_eq_sub_of_has_deriv_at (key1 x) (continuous.continuous_on _),
          continuity        
        end
  ... = (-2 * real.exp (-x ^ 2)) * âˆ« t in 0..x, real.exp (-t ^ 2) : by simp
  ... = -(2 * real.exp (-x ^ 2) * âˆ« t in 0..x, real.exp (-t ^ 2)) : by ring
end

lemma h_zero : h 0 = real.pi / 4 :=
begin
  change (âˆ« t in 0..1, real.exp (-(1 + t^2) * 0^2) / (1 + t^2)) + 
    (âˆ« (t : â„) in 0..0, real.exp (-t^2))^2 = real.pi / 4,
  rw [integral_same, zero_pow zero_lt_two, add_zero],
  conv_lhs {congr, funext, rw mul_zero, rw real.exp_zero},
  convert integral_eq_sub_of_has_deriv_at (Î» t _, t.has_deriv_at_arctan) 
    (continuous_const.div _ _).continuous_on,
  { rw [real.arctan_one, real.arctan_zero, sub_zero] },
  { continuity },
  { intro t,
    linarith [pow_two_nonneg t] } 
end

lemma h_eq : h = (Î» x, real.pi / 4) := 
funext $ Î» x, h_zero â–¸ (is_const_of_deriv_eq_zero (Î» t, (has_deriv_at_h t).differentiable_at) 
  (Î» t, (has_deriv_at_h t).deriv) x 0)

lemma g_le_key (x : â„) (hx : 1 â‰¤ x) :
  (Î» t, (real.exp (-(1+t^2)*x^2))/(1+t^2)) â‰¤ (Î» t, real.exp (-x)) :=
assume t,
have keyâ‚ : 1 â‰¤ 1 + t^2,
  from (le_add_iff_nonneg_right 1).mpr (pow_two_nonneg t),
calc (real.exp (-(1+t^2)*x^2))/(1+t^2) 
      â‰¤ (real.exp (-(1+t^2)*x^2))/1 : div_le_div_of_le_left 
          (real.exp_pos _).le zero_lt_one keyâ‚
  ... = real.exp (-(1+t^2)*x^2) : div_one _
  ... = real.exp ((1+t^2)*(-x^2)) : congr_arg real.exp (by ring)
  ... â‰¤ real.exp (1*(-x^2)) : real.exp_monotone 
          (mul_mono_nonpos (neg_nonpos.mpr $ pow_two_nonneg x) keyâ‚)
  ... = real.exp (-x^2) : by rw one_mul
  ... = real.exp (x*(-x)) : by ring_nf
  ... â‰¤ real.exp (1*(-x)) : real.exp_monotone
          (mul_mono_nonpos (neg_nonpos.mpr $ zero_le_one.trans hx) hx)
  ... = real.exp (-x) : by rw one_mul

lemma g_le : g â‰¤á¶ [at_top] (Î» x, real.exp (-x)) :=
begin
  dsimp [g],
  refine ((eventually_ge_at_top 1).mono $ Î» x hx, _),
  convert interval_integral.integral_mono (zero_le_one : (0 : â„) â‰¤ 1) _ _ (g_le_key x hx),
  { rw interval_integral.integral_const,
    simp },
  { refine (continuous.div _ _ _).interval_integrable 0 1,
    { continuity },
    { continuity },
    { intro x, linarith [pow_two_nonneg x] } },
  { exact continuous.interval_integrable (by continuity) 0 1 },
end

lemma g_tendsto : tendsto g at_top (ğ“ 0) := 
begin
  refine tendsto_of_tendsto_of_tendsto_of_le_of_le' tendsto_const_nhds 
    (real.tendsto_exp_at_bot.comp tendsto_neg_at_top_at_bot) 
    (eventually_of_forall $ Î» x, _) g_le,
  dsimp [g],
  rw integral_of_le,
  { refine integral_nonneg (Î» t, _),
    exact div_nonneg (real.exp_pos _).le 
      (zero_le_one.trans $ (le_add_iff_nonneg_right 1).mpr (pow_two_nonneg t)) },
  { exact zero_le_one }
end

lemma f_square_tendsto : tendsto (f^2) at_top (ğ“ $ real.pi/4) :=
begin
  have : f^2 = h - g,
  { ext, simp [h] },
  rw [this, â† sub_zero (real.pi/4), h_eq],
  exact tendsto_const_nhds.sub g_tendsto
end

lemma f_tendsto : tendsto f at_top (ğ“ $ real.pi.sqrt / 2) :=
begin
  rw [â† real.sqrt_sq zero_le_two, â† real.sqrt_div real.pi_pos.le],
  norm_num,
  refine f_square_tendsto.sqrt.congr' _,
  refine (eventually_ge_at_top 0).mono (Î» x hx, real.sqrt_sq _),
  dsimp [f],
  rw integral_of_le hx,
  refine integral_nonneg (Î» t, (real.exp_pos _).le),
end

lemma tendsto_gauss_integral_symm_interval : 
  tendsto (Î» x, âˆ« t in (-x)..x, real.exp (-t^2)) at_top (ğ“ real.pi.sqrt) :=
begin
  convert â† tendsto.const_mul 2 f_tendsto,
  { ext x,
    rw [two_mul, â† integral_add_adjacent_intervals 
      (continuous_gauss.interval_integrable (-x) 0) 
      (continuous_gauss.interval_integrable 0 x)],
    refine congr_arg2 (+) _ rfl,
    conv in (real.exp _) {rw â† neg_sq, change (Î» t, real.exp (-t^2)) (-t)},
    rw [integral_comp_neg (Î» t, real.exp (-t^2)), neg_zero],
    all_goals {apply_instance} },
  { linarith }
end

lemma gauss_integrable : integrable (Î» x, real.exp (-x^2)) :=
begin
  refine integrable_of_interval_integral_norm_tendsto at_top_countably_generated_of_archimedean 
    (continuous_gauss.ae_measurable _) real.pi.sqrt 
    (Î» i, continuous_gauss.integrable_on_Icc.mono_set Ioc_subset_Icc_self) 
    tendsto_neg_at_top_at_bot tendsto_id _,
  conv in (norm _) {rw real.norm_of_nonneg (-x^2).exp_pos.le},
  exact tendsto_gauss_integral_symm_interval
end

lemma gauss_integral : âˆ« x : â„, real.exp (-x^2) = real.pi.sqrt :=
begin
  have := interval_integral_tendsto_integral at_top_countably_generated_of_archimedean
    (continuous_gauss.ae_measurable _) gauss_integrable tendsto_neg_at_top_at_bot tendsto_id,
  exact tendsto_nhds_unique this tendsto_gauss_integral_symm_interval,
end